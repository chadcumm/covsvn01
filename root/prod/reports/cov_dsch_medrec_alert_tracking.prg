 
 
/*****************************************************************************
  Covenant Health Information Technology
  Knoxville, Tennessee
******************************************************************************
 
	Author:			Geetha Paramasivam
	Date Written:		Feb'22
	Solution:			All
	Source file name:	      cov_dsch_medrec_alert_tracking.prg
	Object name:		Nursing/Case management
	Request#:			10022
 	Program purpose:	      Part of Discharge Med rec rule
 	Executing from:		Rule
  	Special Notes:
 
******************************************************************************
  GENERATED MODIFICATION CONTROL LOG
******************************************************************************
 
Mod Date	Developer			Comment
----------	--------------------	------------------------------------------
 
******************************************************************************/
 
 
drop program cov_dsch_medrec_alert_tracking:dba go
create program cov_dsch_medrec_alert_tracking:dba
 
prompt
	"Output to File/Printer/MINE" = "MINE"       ;* Enter or select the printer or file name to send this report to.
	, "Start Discharged Date/Time" = "SYSDATE"
	, "End Discharged Date/Time" = "SYSDATE"
	, "Select Facility" = 0
 
with OUTDEV, start_datetime, end_datetime, facility_list
 
 
/**************************************************************
; VARIABLE DECLARATION
**************************************************************/
 
declare disch_instruction_var  = f8 with constant(uar_get_code_by("DISPLAY", 72, "Discharge Instructions")),protect
declare disch_summary_var      = f8 with constant(uar_get_code_by("DISPLAY", 72, "Discharge Summary")),protect
declare disch_medrec_alrt_var  = f8 with constant(uar_get_code_by("DISPLAY", 72, "Discharge Medrec Alteration Alert")),protect
 
declare encntrid_var = f8 with noconstant(0.0), protect
declare personid_var = f8 with noconstant(0.0), protect
declare userid_var    = f8 with noconstant(0.0), protect
declare log_message = vc with noconstant('')
declare log_misc1   = vc with noconstant('')
declare alert_id_var  = f8 with noconstant(0.0)
 
/**************************************************************
; CCL SCRIPT STARTS HERE
**************************************************************/
 
Record pat(
	1 rec_cnt = i4
	1 plist[*]
		2 facility = vc
		2 fin = vc
		2 pat_name = vc
		2 encntrid = f8
		2 patientid = f8
		2 dschdoc[*]
			3 user_id_print_dsch_inst = f8
			3 user_name_print_dsch_inst = vc
			3 dsch_inst_print_dq_dt = f8
			3 dsch_inst_print_str_dt = vc
			3 user_id_print_dsch_sum = f8
			3 user_name_print_dsch_sum = vc
			3 dsch_sum_print_dq_dt = f8
			3 dsch_sum_print_str_dt = vc
		2 alert[*]
			3 alertid = f8
			3 alrt_type = vc
			3 alrt_dt = dq8
			3 users_cm_alrt_received = vc
			3 users_cm_acpt_alrt = vc ;user + dt/tm
			3 users_nu_alrt_received = vc
			3 users_nu_acpt_alrt = vc ;user + dt/tm
			
	)
 
 
;==========================================================================================
;Alerts generated by the rule
 
select into $outdev
 
e.encntr_id, eh.send_dt_tm, eh.alert_id, eh.*
 
from eks_alert_esc_hist eh
	,encounter e
 
plan e where e.loc_facility_cd = $facility_list
	and e.active_ind = 1
 
join eh where eh.encntr_id = e.encntr_id
	and eh.send_dt_tm between cnvtdatetime($start_datetime) and cnvtdatetime($end_datetime)
	and eh.msg_type_cd = value(uar_get_code_by("DISPLAY", 30420, "Notify"))
	and eh.subject_text IN('*Discharge Medication Alert - Case Management', '*Discharge Medication Alert - Nursing')
	and eh.alert_source IN('COV_DSCH_MEDREC_TSK', 'COV_DSCH_MEDREC_TSK_REMIN')
 
order by eh.encntr_id, eh.alert_id
 
Head report
	cnt = 0
Head eh.encntr_id
	cnt += 1
	if (mod(cnt,10) = 1 or cnt = 1)
		stat = alterlist(pat->plist, cnt + 9)
	endif
	pat->rec_cnt = cnt
	pat->plist[cnt].facility = uar_get_code_display(e.loc_facility_cd)
	pat->plist[cnt].encntrid = eh.encntr_id
	pat->plist[cnt].patientid = e.person_id
	acnt = 0
Head eh.alert_id
	acnt += 1
	if (mod(acnt,10) = 1 or acnt = 1)
		stat = alterlist(pat->plist[cnt].alert, acnt + 9)
	endif
	pat->plist[cnt].alert[acnt].alertid = eh.alert_id
	pat->plist[cnt].alert[acnt].alrt_type = eh.subject_text
	pat->plist[cnt].alert[acnt].alrt_dt = eh.send_dt_tm
Foot eh.encntr_id
	stat = alterlist(pat->plist[cnt].alert, acnt)
Foot report
	stat = alterlist(pat->plist, cnt)
 
with nocounter
 
if(pat->rec_cnt > 0)
 
;==========================================================================================
;Discharge Inst & Summary
 
select into $outdev
ce.encntr_id, ce.event_end_dt_tm, ce.event_cd, ce.performed_prsnl_id, ce.*
 
from (dummyt d1 with seq = pat->rec_cnt)
	,clinical_event ce
	, prsnl pr
 
plan d1
 
join ce where ce.encntr_id = pat->plist[d1.seq].encntrid
	and ce.event_cd in(disch_instruction_var, disch_summary_var)
	and ce.valid_until_dt_tm = cnvtdatetime ("31-DEC-2100 00:00:00" )
	and ce.result_status_cd IN (23.00, 34.00, 25.00, 35.00)
	and ce.view_level = 1
 
join pr where pr.person_id = ce.performed_prsnl_id
 
order by ce.encntr_id, ce.event_id
 
Head ce.encntr_id
	icnt = 0
	idx = 0
      idx = locateval(icnt ,1 ,pat->rec_cnt ,ce.encntr_id ,pat->plist[icnt].encntrid)
      dcnt = 0
Head ce.event_id
	if(idx > 0)
		dcnt += 1
		if (mod(dcnt,10) = 1 or dcnt = 1)
			stat = alterlist(pat->plist[idx].dschdoc, dcnt + 9)
		endif
 
		case(ce.event_cd)
			of disch_instruction_var:
				pat->plist[idx].dschdoc[dcnt].dsch_inst_print_dq_dt = ce.event_end_dt_tm
				pat->plist[idx].dschdoc[dcnt].dsch_inst_print_str_dt = format(ce.event_end_dt_tm, 'mm-dd/yy hh:mm:ss;;q')
				pat->plist[idx].dschdoc[dcnt].user_id_print_dsch_inst = ce.performed_prsnl_id
				pat->plist[idx].dschdoc[dcnt].user_name_print_dsch_inst = pr.name_full_formatted
			of disch_summary_var:
				pat->plist[idx].dschdoc[dcnt].dsch_sum_print_dq_dt = ce.event_end_dt_tm
				pat->plist[idx].dschdoc[dcnt].dsch_sum_print_str_dt = format(ce.event_end_dt_tm, 'mm-dd/yy hh:mm:ss;;q')
				pat->plist[idx].dschdoc[dcnt].user_id_print_dsch_sum = ce.performed_prsnl_id
				pat->plist[idx].dschdoc[dcnt].user_name_print_dsch_sum = pr.name_full_formatted
		endcase
	endif
Foot ce.encntr_id
	stat = alterlist(pat->plist[idx].dschdoc, dcnt)
with nocounter
 
;==========================================================================================
;Users received the alert - cm
 
select into $outdev
  eh.*
 
from (dummyt d1 with seq = pat->rec_cnt)
	,(dummyt   d2  with seq = 1)
	,eks_alert_esc_hist eh
	,prsnl pr
 
plan d1 where maxrec(d2, size(pat->plist[d1.seq].alert, 5))
join d2
 
join eh where eh.encntr_id = pat->plist[d1.seq].encntrid
	and eh.alert_id = pat->plist[d1.seq].alert[d2.seq].alertid
			and eh.parent_entity_id != null
			and eh.parent_entity_name = 'PERSON'
			and eh.subject_text = '*Discharge Medication Alert - Case Management'
 
join pr where pr.person_id = eh.parent_entity_id
 
order by eh.encntr_id, eh.alert_id, eh.parent_entity_id
 
Head eh.encntr_id
	icnt = 0
	idx = 0
      idx = locateval(icnt ,1 ,pat->rec_cnt ,eh.encntr_id ,pat->plist[icnt].encntrid)
Head eh.alert_id
	users_alerted = fillstring(2000," ")
	icnt1 = 0
	idx1 = 0
      idx1 = locateval(icnt1 ,1 ,pat->rec_cnt ,eh.alert_id ,pat->plist[idx].alert[icnt1].alertid )
Head eh.parent_entity_id
	users_alerted = build2(trim(users_alerted),'[' ,trim(pr.name_full_formatted),']',',')
Foot eh.alert_id
	pat->plist[idx].alert[idx1].users_cm_alrt_received = replace(trim(users_alerted),",","",2)
with nocounter

;==========================================================================================
;Users received the alert - nu
 
select into $outdev
  eh.*
 
from (dummyt d1 with seq = pat->rec_cnt)
	,(dummyt   d2  with seq = 1)
	,eks_alert_esc_hist eh
	,prsnl pr
 
plan d1 where maxrec(d2, size(pat->plist[d1.seq].alert, 5))
join d2
 
join eh where eh.encntr_id = pat->plist[d1.seq].encntrid
	and eh.alert_id = pat->plist[d1.seq].alert[d2.seq].alertid
			and eh.parent_entity_id != null
			and eh.parent_entity_name = 'PERSON'
			and eh.subject_text = '*Discharge Medication Alert - Nursing'
 
join pr where pr.person_id = eh.parent_entity_id
 
order by eh.encntr_id, eh.alert_id, eh.parent_entity_id
 
Head eh.encntr_id
	icnt = 0
	idx = 0
      idx = locateval(icnt ,1 ,pat->rec_cnt ,eh.encntr_id ,pat->plist[icnt].encntrid)
Head eh.alert_id
	users_alerted = fillstring(2000," ")
	icnt1 = 0
	idx1 = 0
      idx1 = locateval(icnt1 ,1 ,pat->rec_cnt ,eh.alert_id ,pat->plist[idx].alert[icnt1].alertid )
Head eh.parent_entity_id
	users_alerted = build2(trim(users_alerted),'[' ,trim(pr.name_full_formatted),']',',')
Foot eh.alert_id
	pat->plist[idx].alert[idx1].users_nu_alrt_received = replace(trim(users_alerted),",","",2)
with nocounter


;==========================================================================================

;Users Accept Alert - Case Mgmnt
 
select into $outdev
alrtid = pat->plist[d1.seq].alert[d2.seq].alertid, alrt_dt = pat->plist[d1.seq].alert[d2.seq].alrt_dt ';;q'
, ce.encntr_id, ce.event_cd, ce.result_val, veri_dt = format(ce.verified_dt_tm, 'mm-dd/yy hh:mm:ss;;q')
 
from (dummyt d1 with seq = pat->rec_cnt)
	,(dummyt   d2  with seq = 1)
	,clinical_event ce
	,prsnl pr
 
plan d1 where maxrec(d2, size(pat->plist[d1.seq].alert, 5))
join d2
 
join ce where ce.encntr_id = pat->plist[d1.seq].encntrid
	and ce.event_cd = disch_medrec_alrt_var
	and ce.result_val = 'Case Management Alert*'
	and (cnvtreal(trim(substring(25, 7, ce.result_val))) = pat->plist[d1.seq].alert[d2.seq].alertid)
 
join pr where pr.person_id = ce.verified_prsnl_id
 
order by ce.encntr_id, alrtid, ce.verified_prsnl_id
 
Head ce.encntr_id
	icnt = 0
	idx = 0
      idx = locateval(icnt ,1 ,pat->rec_cnt ,ce.encntr_id ,pat->plist[icnt].encntrid)
Head alrtid
	users_alert_accept = fillstring(2000," ")
	icnt1 = 0
	idx1 = 0
      idx1 = locateval(icnt1 ,1 ,pat->rec_cnt ,alrtid ,pat->plist[idx].alert[icnt1].alertid )
Head ce.verified_prsnl_id
	users_alert_accept = build2(trim(users_alert_accept),'[' ,trim(pr.name_full_formatted),' - ',veri_dt,']',',')
Foot alrtid
	pat->plist[idx].alert[idx1].users_cm_acpt_alrt = replace(trim(users_alert_accept),",","",2)
with nocounter


;==========================================================================================

;Users Accept Alert - Nursing
 
select into $outdev
alrtid = pat->plist[d1.seq].alert[d2.seq].alertid, alrt_dt = pat->plist[d1.seq].alert[d2.seq].alrt_dt ';;q'
, ce.encntr_id, ce.event_cd, ce.result_val, veri_dt = format(ce.verified_dt_tm, 'mm-dd/yy hh:mm:ss;;q')
 
from (dummyt d1 with seq = pat->rec_cnt)
	,(dummyt   d2  with seq = 1)
	,clinical_event ce
	,prsnl pr
 
plan d1 where maxrec(d2, size(pat->plist[d1.seq].alert, 5))
join d2
 
join ce where ce.encntr_id = pat->plist[d1.seq].encntrid
	and ce.event_cd = disch_medrec_alrt_var
	and ce.result_val = 'Nursing Alert*'
	and (cnvtreal(trim(substring(17, 7, ce.result_val))) = pat->plist[d1.seq].alert[d2.seq].alertid)
 
join pr where pr.person_id = ce.verified_prsnl_id
 
order by ce.encntr_id, alrtid, ce.verified_prsnl_id
 
Head ce.encntr_id
	icnt = 0
	idx = 0
      idx = locateval(icnt ,1 ,pat->rec_cnt ,ce.encntr_id ,pat->plist[icnt].encntrid)
Head alrtid
	users_alert_accept = fillstring(2000," ")
	icnt1 = 0
	idx1 = 0
      idx1 = locateval(icnt1 ,1 ,pat->rec_cnt ,alrtid ,pat->plist[idx].alert[icnt1].alertid )
Head ce.verified_prsnl_id
	users_alert_accept = build2(trim(users_alert_accept),'[' ,trim(pr.name_full_formatted),' - ',veri_dt,']',',')
Foot alrtid
	pat->plist[idx].alert[idx1].users_nu_acpt_alrt = replace(trim(users_alert_accept),",","",2)
with nocounter

;--------------------------------------------------------------------------------------------------------------------------------
;Demographic
 
select into $outdev
 
from (dummyt d with seq = value(size(pat->plist, 5)))
	, person p
	, encntr_alias ea
 
plan d
 
join p where p.person_id = pat->plist[d.seq].patientid
	and p.active_ind = 1
 
join ea where ea.encntr_id = pat->plist[d.seq].encntrid
	and ea.active_ind = 1
	and ea.encntr_alias_type_cd = 1077
 
order by ea.encntr_id
 
Head ea.encntr_id
	idx = 0
	icnt = 0
      idx = locateval(icnt ,1 ,size(pat->plist,5) ,ea.encntr_id ,pat->plist[icnt].encntrid)
	while(idx > 0)
		pat->plist[idx].pat_name = p.name_full_formatted
		pat->plist[idx].fin = trim(ea.alias)
		idx = locateval(icnt ,(idx+1) ,size(pat->plist,5) ,ea.encntr_id ,pat->plist[icnt].encntrid)
	endwhile
 
with nocounter

 
;==========================================================================================
 
call echorecord(pat)
;==========================================================================================
 
 select into $outdev
	facility = trim(substring(1, 30, pat->plist[d1.seq].facility))
	, fin = trim(substring(1, 30, pat->plist[d1.seq].fin))
	, patient_name = trim(substring(1, 50, pat->plist[d1.seq].pat_name))
	, alert_type = trim(substring(1, 300, pat->plist[d1.seq].alert[d3.seq].alrt_type))
	, alert_dt = format(pat->plist[d1.seq].alert[d3.seq].alrt_dt, 'dd/mm/yy hh:mm;;q')
	, alert_received_casemgmnt = trim(substring(1, 3000, pat->plist[d1.seq].alert[d3.seq].users_cm_alrt_received))
	, alert_acepted_casemgmnt = trim(substring(1, 3000, pat->plist[d1.seq].alert[d3.seq].users_cm_acpt_alrt))
	, user_print_dsch_sum = trim(substring(1, 300, pat->plist[d1.seq].dschdoc[d2.seq].user_name_print_dsch_sum))
	, dsch_sum_print_dt = trim(substring(1, 30, pat->plist[d1.seq].dschdoc[d2.seq].dsch_sum_print_str_dt))
	, alert_received_nursing = trim(substring(1, 3000, pat->plist[d1.seq].alert[d3.seq].users_nu_alrt_received))
	, alert_acepted_nursing = trim(substring(1, 3000, pat->plist[d1.seq].alert[d3.seq].users_nu_acpt_alrt))
	, user_print_dsch_inst = trim(substring(1, 3000, pat->plist[d1.seq].dschdoc[d2.seq].user_name_print_dsch_inst))
	, dsch_inst_print_dt = trim(substring(1, 30, pat->plist[d1.seq].dschdoc[d2.seq].dsch_inst_print_str_dt))

from
	(dummyt   d1  with seq = size(pat->plist, 5))
	, (dummyt   d2  with seq = 1)
	, (dummyt   d3  with seq = 1)

plan d1 where maxrec(d2, size(pat->plist[d1.seq].dschdoc, 5))
	and maxrec(d3, size(pat->plist[d1.seq].alert, 5))
join d2 
join d3

order by facility, fin, alert_dt

with nocounter, separator=" ", format

 
 
 
endif ;rec_cnt
 
#exitscript
 
end go
 
 
;with nocounter, separator=" ", format, uar_code(d,1), format(date,"mm-dd-yyyy hh:mm:ss;;d"), time = 120
;go to exitscript
 
/* 
select *
from eks_notifications t, eks_notify_persn_r l, long_blob m
plan l where l.person_id = 12735933.00 ;12428721.00
;and l.read_ind != 2
join t where t.notification_id = l.notification_id
join m where m.parent_entity_name= "EKS_NOTIFICATIONS"
and m.parent_entity_id=t.notification_id
order by t.updt_dt_tm desc
 
with nocounter, separator=" ", format, uar_code(d,1), format(date,"mm-dd-yyyy hh:mm:ss;;d"), time = 180;, maxrow = 1000

