/*****************************************************************************
  Covenant Health Information Technology
  Knoxville, Tennessee
******************************************************************************
	Author:			Geetha Paramasivam
	Date Written:		Oct'2022
	Solution:			BH
	Source file name:	      cov_bh_antipsychomed_audit.prg
	Object name:		cov_bh_antipsychomed_audit
	Request#:			12959
	Program purpose:	      
	Executing from:		DA2
 	Special Notes:
 
******************************************************************************
  GENERATED MODIFICATION CONTROL LOG
******************************************************************************
 
Mod Date		Developer			Comment
-------------------------------------------------------------------------------------------------------


-------------------------------------------------------------------------------------------------------*/

drop program cov_bh_antipsychomed_audit:dba go
create program cov_bh_antipsychomed_audit:dba

prompt 
	"Output to File/Printer/MINE" = "MINE"   ;* Enter or select the printer or file name to send this report to.
	, "Start Alert Date/Time" = "SYSDATE"
	, "End Alert Date/Time" = "SYSDATE"
	, "Select Facility" = 0 

with OUTDEV, start_datetime, end_datetime, facility_list


/**************************************************************
; DVDev DECLARED VARIABLES
**************************************************************/

declare antipsy_med_var  = f8 with constant(value(uar_get_code_by("DISPLAY", 72, 'Discharge Antipsychotic Medications'))),protect  

/**************************************************************
; DVDev Start Coding
**************************************************************/

Record pat(
	1 rec_cnt = i4
	1 plist[*]
		2 facility = vc
		2 personid = f8
		2 encntrid = f8
		2 fin = vc
		2 pat_name = vc
		2 disch_dt = vc
		2 alert_pr_id = f8
		2 pr_received_alert = vc
		2 alert_dt = dq8
		2 dta_response = vc
		2 dta_response_dt = vc
	)
 
;----------------------------------------------------------------------------------------------------------
;Rule action

select into $outdev

emad.encntr_id, alert_date = ema.begin_dt_tm ';;q'

from eks_module_audit_det emad, eks_module_audit ema, encounter e

plan e where e.loc_facility_cd = $facility_list
	and e.active_ind = 1

join emad where emad.encntr_id = e.encntr_id
	and emad.template_type = 'A'
	and emad.template_return = 100

join ema where ema.rec_id = emad.module_audit_id 
	and ema.begin_dt_tm between cnvtdatetime($start_datetime) and cnvtdatetime($end_datetime)
	and ema.module_name = 'COV_BH_ANTIPSYCH_DSCHG_OR'
	
order by emad.encntr_id

Head report
	cnt = 0
Head emad.encntr_id
	cnt += 1
	pat->rec_cnt = cnt
	if (mod(cnt,10) = 1 or cnt = 1)
		stat = alterlist(pat->plist, cnt + 9)
	endif
	pat->plist[cnt].encntrid = emad.encntr_id
	pat->plist[cnt].personid = emad.person_id		
	pat->plist[cnt].alert_dt = ema.begin_dt_tm
	pat->plist[cnt].alert_pr_id = emad.updt_id
	pat->plist[cnt].facility = uar_get_code_display(e.loc_facility_cd)
Foot report
	stat = alterlist(pat->plist, cnt)

with nocounter

if(pat->rec_cnt > 0)
;---------------------------------------------------------------------------------------------------------------
;DTA response

select into $outdev

ce.encntr_id, ce.event_cd, ce.result_val, ce.event_end_dt_tm, alrt_dt = pat->plist[d.seq].alert_dt ';;q'

from (dummyt d with seq = pat->rec_cnt)
	, clinical_event ce
	
plan d

join ce where ce.encntr_id = pat->plist[d.seq].encntrid
	and ce.event_cd = antipsy_med_var
	and ce.valid_until_dt_tm = cnvtdatetime ("31-DEC-2100 00:00:00" )
	and ce.result_status_cd IN (23.00, 34.00, 25.00, 35.00)
	and ce.updt_id = pat->plist[d.seq].alert_pr_id
	;and ce.event_end_dt_tm >= cnvtdatetime(pat->plist[d.seq].alert_dt);doc found in different dt/tm
	and ce.event_id = (select max(ce1.event_id) from clinical_event ce1
					where ce1.encntr_id = ce.encntr_id
					and ce1.event_cd = ce.event_cd
					and ce1.updt_id = ce.updt_id
					and ce1.valid_until_dt_tm = cnvtdatetime ("31-DEC-2100 00:00:00" )
					and ce1.result_status_cd IN (23.00, 34.00, 25.00, 35.00)
					group by ce1.encntr_id, ce1.event_cd)

order by ce.encntr_id					

Head ce.encntr_id
	idx = 0
	icnt = 0
      idx = locateval(icnt ,1 ,size(pat->plist,5) ,ce.encntr_id ,pat->plist[icnt].encntrid)
Detail
	pat->plist[idx].dta_response = ce.result_val	
	pat->plist[idx].dta_response_dt = format(ce.event_end_dt_tm, 'mm/dd/yy hh:mm:ss ;;q')
with nocounter

;------------------------------------------------------------------------------------------------------------
;Demographic
select into $outdev

from (dummyt d with seq = pat->rec_cnt)
	, encntr_alias ea
	, person p
	
plan d

join ea where ea.encntr_id = pat->plist[d.seq].encntrid 
	and ea.encntr_alias_type_cd = 1077

join p where p.person_id = pat->plist[d.seq].personid

order by ea.encntr_id

Head ea.encntr_id
	idx = 0
	icnt = 0
      idx = locateval(icnt ,1 ,size(pat->plist,5) ,ea.encntr_id ,pat->plist[icnt].encntrid)
Detail
	pat->plist[idx].fin = trim(ea.alias)
	pat->plist[idx].pat_name = p.name_full_formatted
with nocounter

;------------------------------------------------------------------------------------------------------------
;PRSNL
select into $outdev

from (dummyt d with seq = pat->rec_cnt)
	, prsnl pr
	
plan d

join pr where pr.person_id = pat->plist[d.seq].alert_pr_id

order by pr.person_id

Head pr.person_id
	idx = 0
	icnt = 0
      idx = locateval(icnt ,1 ,size(pat->plist,5) ,pr.person_id ,pat->plist[icnt].alert_pr_id)
while(idx > 0)
	pat->plist[idx].pr_received_alert = pr.name_full_formatted
	idx = locateval(icnt ,(idx+1) ,size(pat->plist,5) ,pr.person_id ,pat->plist[icnt].alert_pr_id)
endwhile	
	
with nocounter

;----------------------------------------------------------------------------------------------------------	

call echorecord(pat)

;----------------------------------------------------------------------------------------------------------	

select into $outdev
	facility = substring(1, 30, pat->plist[d1.seq].facility)
	, fin = substring(1, 30, pat->plist[d1.seq].fin)
	, patient_name = substring(1, 80, pat->plist[d1.seq].pat_name)
	, alert_dt = pat->plist[d1.seq].alert_dt ';;q'
	, provider_received_alert = substring(1, 100, pat->plist[d1.seq].pr_received_alert)
	, discharge_antipsychotic_documentation = substring(1, 500, pat->plist[d1.seq].dta_response)
	, documentation_dt = substring(1, 30, pat->plist[d1.seq].dta_response_dt)

from
	(dummyt   d1  with seq = size(pat->plist, 5))

plan d1

order by alert_dt

with nocounter, separator=" ", format



#exitscript

endif ; rec_cnt

end
go




/*
with nocounter, separator=" ", format, format(date,"mm-dd-yyyy hh:mm:ss;;d"), uar_code(d,1), time = 360


select 
emad.encntr_id, alert_dt = ema.begin_dt_tm ';;q'
, ema.module_name, emad.logging,emad.template_name,emad.template_alias, emad.template_return, emad.template_number;, ema.*, emad.*
from eks_module_audit_det emad, eks_module_audit ema
where emad.module_audit_id = ema.rec_id
and emad.encntr_id =   129655973.00
and ema.module_name = 'COV_BH_ANTIPSYCH_DSCHG_OR'
and emad.template_type = 'A'
and emad.template_return = 100
;and ema.updt_dt_tm >= cnvtdatetime('01-JAN-2022 00:00:00')
order by emad.template_number
with nocounter, separator=" ", format, format(date,"mm-dd-yyyy hh:mm:ss;;d"), uar_code(d,1), time = 360;, maxrow = 10000

  131756640.00
  132117237.00

*/















