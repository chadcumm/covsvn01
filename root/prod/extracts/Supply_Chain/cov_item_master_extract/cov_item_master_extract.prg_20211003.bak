/*****************************************************************************
  Covenant Health Information Technology
  Knoxville, Tennessee
******************************************************************************
 
	Author:				Marty Blair
	Date Written:		03/30/2020
	Solution:
	Source file name:	cov_item_master_extract.prg
	Object name:		cov_item_master_extract
	Request #:
 
	Program purpose:
 
	Executing from:		CCL
 
 	Special Notes:		Called by ccl program(s).
 
******************************************************************************
  GENERATED MODIFICATION CONTROL LOG
******************************************************************************
 
Mod 	Date	    Developer				Comment
---     ----------  --------------------    ----------------------------------
000 	09/12/2020  Chad Cummings           Initial Release
001 	04/20/2021  Chad Cummings           Added addtional UPN logic
002     08/16/2021  Dawn Greer, DBA         CR 10232 - Added ASTREAM path
003     10/03/2021  Dawn Greer, DBA         CR 10232 - Took out time portion on 
                                            file name and formatting.
******************************************************************************/
drop program cov_item_master_extract go
create program cov_item_master_extract
 
IF (NOT(validate(reply,0)))
RECORD  reply
(
	1 text = vc
	1 status_data
	 2 status = c1
	 2 subeventstatus[1]
	  3 operationname = c15
	  3 operationstatus = c1
	  3 targetobjectname = c15
	  3 targetobjectvalue = c100
)
ENDIF
 
SET reply->status_data.status = "F"
 
RECORD t_rec
(
	1 cnt					= i4
	1 output_filename 		= vc
	1 output_var			= vc
)
 
SET t_rec->output_filename = CONCAT(TRIM(CNVTLOWER(curprog)),"_",TRIM(FORMAT(SYSDATE,"yyyymmdd;;d")),".txt")
SET t_rec->output_var = CONCAT("cer_temp:",TRIM(t_rec->output_filename))
 
SELECT DISTINCT INTO value(t_rec->output_var)
	 charge_nbr = ot.value
	,exp_dt_ind = df.udi_exp_date_ind
	,serial_ind = df.udi_serial_nbr_ind
	,lot_ind = df.udi_lot_nbr_ind
	,mfg_dt_ind = df.udi_mfr_date_ind
	,sub_acct = csub.display
	,created_by = pe.name_full_formatted
	,item_nbr = it.stock_nbr
	,item_descrp = it.description
	,hcpcs = bim2.key6
	,upn = ob.value
	,price_schedule = ps.price_sched_desc
	,supply_cost = ic.cost
	,organization = O.ORG_NAME
	,CDM = bim.key6
	,charge_ind = df.chargeable_ind
	,UOM = cuom.display
	,classification = it.class_name
	,mfg_catg_nbr = ocat.value
	,manufacturer = cv.display
	,created_dt_tm = it.create_dt_tm
FROM mm_omf_item_master it
	,item_definition df
	,item_master ma
	,item_location_cost ic
	,location loc
	,organization o
	,object_identifier_index ob
	,identifier id
	,package_type pk
	,object_identifier_index ot
	,object_identifier_index ocat
	,code_value cv
	,bill_item bi
	,bill_item b2
	,bill_item_modifier bim
	,bill_item_modifier bim2
	,prsnl pe
	,price_sched_items psi
	,price_sched ps
	,code_value cuom
	,code_value csub
PLAN IT WHERE it.active_ind = 1
JOIN df WHERE df.item_id = it.item_master_id
	AND df.active_ind = 1
JOIN ma WHERE ma.item_id = it.item_master_id
JOIN ic WHERE ic.item_id  = it.item_master_id
	AND ic.cost_type_cd = 24492892
JOIN loc WHERE loc.location_cd = ic.location_cd
	AND loc.active_ind = 1
JOIN o WHERE o.organization_id = loc.organization_id
	AND o.active_ind = 1
JOIN ob WHERE OUTERJOIN(it.item_master_id) = ob.object_id
	AND ob.identifier_type_cd = OUTERJOIN(638956)
	AND TEXTLEN(ob.value) > OUTERJOIN(6)
JOIN id WHERE OUTERJOIN(ob.identifier_id) = id.identifier_id
	AND id.replaced_upn_id = OUTERJOIN (0) ;001
JOIN pk WHERE OUTERJOIN(id.package_type_id) = pk.package_type_id
	AND pk.base_package_type_ind = OUTERJOIN(1)
JOIN ot WHERE OUTERJOIN(it.item_master_id) = ot.object_id
	AND ot.identifier_type_cd  = OUTERJOIN(3096)
JOIN ocat WHERE it.item_master_id = ocat.object_id
	AND ocat.identifier_type_cd  = 3103
	AND ocat.active_ind = 1
JOIN cv WHERE ocat.vendor_manf_cd = cv.code_value
	AND cv.active_ind = 1
JOIN bi WHERE it.item_master_id = bi.ext_parent_reference_id
    AND bi.active_ind = 1
JOIN bim WHERE OUTERJOIN(bi.bill_item_id) = bim.bill_item_id
	AND bim.key1_id = OUTERJOIN(2556027807)
	AND bim.active_ind = OUTERJOIN(1)
	AND bim.end_effective_dt_tm > OUTERJOIN(SYSDATE)
JOIN b2 WHERE it.item_master_id = b2.ext_parent_reference_id
	AND b2.active_ind = 1
JOIN bim2 WHERE OUTERJOIN(b2.bill_item_id) = bim2.bill_item_id
	AND bim2.key1_id = OUTERJOIN(615215)
	AND bim2.active_ind = OUTERJOIN(1)
	AND bim2.end_effective_dt_tm > OUTERJOIN(SYSDATE)
JOIN pe WHERE pe.person_id = it.create_id
JOIN psi WHERE psi.bill_item_id = bi.bill_item_id
	AND psi.active_ind = 1
JOIN ps WHERE ps.price_sched_id = psi.price_sched_id
	AND ps.active_ind = 1
JOIN csub WHERE ma.sub_account_cd = csub.code_value
	AND csub.active_ind = 1
JOIN cuom WHERE it.base_pkg_uom_cd = cuom.code_value
	AND cuom.active_ind = 1
WITH nocounter, pcformat(^"^, ^,^, 1,0),format=stream,formfeed=none,maxcol=32000,format
 
IF (curqual > 0)
	SET reply->status_data.status = "S"
	SET t_rec->cnt = curqual
	EXECUTE cov_astream_file_transfer "cer_temp",replace(t_rec->output_var,"cer_temp:",""),
	"Extracts/SupplyChain","CP" ;002
ELSE
	SET reply->status_data.status = "Z"
ENDIF
 
CALL ECHORECORD(t_rec)
CALL ECHORECORD(reply)
 
END
GO
 
 
 
 
